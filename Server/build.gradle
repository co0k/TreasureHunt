// Configure the Java plugin and the dependencies
// ----------------------------------------------
apply plugin: 'java'

version = '0.1'
sourceCompatibility = 1.7
targetCompatibility = 1.7
repositories {
    mavenCentral()
}

version='0.1'

dependencies {
    compile 'org.slf4j:slf4j-api:1.7.7'
    //compile 'org.slf4j:slf4j-simple:1.7.7'
    compile 'org.slf4j:slf4j-log4j12:1.7.7'
    compile 'org.eclipse.jetty:jetty-server:9.3.0.M2'
    compile 'org.eclipse.jetty:jetty-servlet:9.3.0.M2'
    compile 'org.eclipse.jetty:jetty-websocket:8.1.17.v20150415'
    compile 'org.eclipse.jetty.websocket:websocket-core:9.0.0.M2'
    compile 'org.eclipse.jetty.websocket:websocket-server:9.3.0.M2'
    compile 'org.eclipse.jetty.websocket:websocket-servlet:9.3.0.M2'
    compile 'org.eclipse.jetty.websocket:javax-websocket-server-impl:9.3.0.M2'
    compile 'org.eclipse.jetty.websocket:websocket-parent:9.3.0.M2'
    compile 'org.eclipse.jetty.websocket:websocket-api:9.3.0.M2'
    compile 'javax:javaee-api:7.0'
    compile 'com.thetransactioncompany:jsonrpc2-base:1.34.4'
    compile 'org.jooq:jooq:3.6.0'
    compile 'mysql:mysql-connector-java:+'
    compile 'com.google.code.gson:gson:2.3.1'
    testCompile 'junit:junit:[4,)'
}
task libs(type: Jar, dependsOn: classes) {
    manifest {
        attributes 'Implementation-Title': 'Data structures for the TreasureHunt App',
                'Implementation-Version': version
    }
    baseName = 'TreasureHunt-libs'
    from(sourceSets.main.output) {
        includes = ['data_structures/**', 'communication_controller/json/**']
    }
}
buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.jooq:jooq-codegen:3.6.0'
        classpath 'com.h2database:h2:1.4.177'
        classpath 'mysql:mysql-connector-java:+'
    }
}
tasks.withType(JavaCompile) {
    doFirst {
        if (sourceCompatibility == '1.7' && System.env.JDK7_HOME != null) {
            options.fork = true
            options.bootClasspath = "$System.env.JDK7_HOME/jre/lib/rt.jar"
            options.bootClasspath += "$File.pathSeparator$System.env.JDK7_HOME/jre/lib/jsse.jar"
            // use the line above as an example to add jce.jar
            // and other specific JDK jars
        }
    }
}
task generate << {

    // Use your favourite XML builder to construct the code generation configuration file
    // ----------------------------------------------------------------------------------
    def writer = new StringWriter()
    def xml = new groovy.xml.MarkupBuilder(writer)
            .configuration('xmlns': 'http://www.jooq.org/xsd/jooq-codegen-3.6.0.xsd') {
        jdbc() {
            driver('com.mysql.jdbc.Driver')
            url('jdbc:mysql://127.0.0.1:3306/library')
            user('root')
            password('')
        }
        generator() {
            database() {
                name('org.jooq.util.mysql.MySQLDatabase')
                includes('.*')
                inputSchema('library')
            }
            generate() {
            }
            target() {
                packageName('db.generated')
                directory('Server/src/main/java')
            }
        }
    }

    // Run the code generator
    // ----------------------
    org.jooq.util.GenerationTool.main(
            javax.xml.bind.JAXB.unmarshal(
                    new StringReader(writer.toString()),
                    org.jooq.util.jaxb.Configuration.class
            )
    )
}
task cleanGeneratedFiles << {
    delete fileTree(dir: "src/main/java/db/generated")
}
test {
    testLogging {
        events = ["passed", "standard_error", "failed", "skipped"]
        exceptionFormat 'full'
        showCauses true
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Server for SEPM',
                'Implementation-Version': version,
                'Main-Class': 'frontend.TreasureServer'
    }
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Server for the TreasureHunt App',
                'Implementation-Version': version,
                'Main-Class':  'frontend.TreasureServer'
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it)  }  }
    with jar
}

compileJava.dependsOn(generate)
clean.dependsOn(cleanGeneratedFiles)
assemble.dependsOn(fatJar)
assemble.dependsOn(libs)
